plugins {
    application
    id("com.gradleup.shadow") version "8.3.6"
}

group = "cc.tweaked"
version = "1.0"
val modVersion = "1.116.2"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven("https://maven.squiddev.cc")
        }
        filter {
            includeGroup("cc.tweaked")
        }
    }
}

dependencies {
    implementation("cc.tweaked:cc-tweaked-1.20.1-core:$modVersion")

    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("com.google.guava:guava:33.4.0-jre")

    // Instrumentation
    val otVersion = "1.52.0"
    implementation(platform("io.opentelemetry:opentelemetry-bom:$otVersion"))

    implementation("io.opentelemetry:opentelemetry-api")
    implementation("io.opentelemetry:opentelemetry-api")
    implementation("io.opentelemetry:opentelemetry-sdk")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp") {
        // Disable the okhttp exporter and use the JDK one instead.
        exclude(group = "io.opentelemetry", module = "opentelemetry-exporter-sender-okhttp")
    }
    runtimeOnly("io.opentelemetry:opentelemetry-exporter-sender-jdk")

    implementation("io.opentelemetry:opentelemetry-extension-trace-propagators")

    implementation("io.opentelemetry.semconv:opentelemetry-semconv:1.34.0")

    runtimeOnly("io.opentelemetry.instrumentation:opentelemetry-logback-mdc-1.0:2.17.0-alpha")

    // Logging
    runtimeOnly("ch.qos.logback:logback-core:1.5.18")
    runtimeOnly("ch.qos.logback:logback-classic:1.5.18")
}

application {
    mainClass.set("cc.tweaked.eval.Main")
}

tasks.named<Jar>("jar") {
    manifest {
        attributes(
            mapOf(
                "Implementation-Title" to "eval.tweaked.cc",
                "Implementation-Version" to modVersion,
                "Implementation-Vendor" to "SquidDev",
            )
        )
    }
}

tasks.named<com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar>("shadowJar") {
    mergeServiceFiles()
    minimize() {
        exclude(dependency("ch.qos.logback:.*:.*"))
        exclude(dependency("io.opentelemetry.*:.*:.*"))
    }
}

tasks.withType(AbstractArchiveTask::class.java).configureEach {
    isPreserveFileTimestamps = false
    isReproducibleFileOrder = true
    filePermissions {}
    dirPermissions {}
}
